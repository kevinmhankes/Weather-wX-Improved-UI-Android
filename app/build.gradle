apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    //compileSdkVersion 'android-R'
    compileSdkVersion 29
    buildToolsVersion '29.0.3'    // was 29.0.3

    // need $HOME/.gradle/gradle.properties to look like this
    /*
    RELEASE_STORE_FILE=/home/josh/StudioProjects/android.jks
    RELEASE_STORE_PASSWORD=pw
    RELEASE_KEY_ALIAS=android key
    RELEASE_KEY_PASSWORD=pw
    */

    signingConfigs {
        release {
            storeFile file("../../android.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "joshuatee.wx"
        minSdkVersion 16   // 4.1
        //targetSdkVersion 'R' // 30
        targetSdkVersion 29
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors (2016_02_25 add 23.2 )
        //generatedDensities = []
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        main {
            jni.srcDirs = [] //disable automatic ndk-build
            jniLibs.srcDir 'src/main/libs'
        }
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = "../../../android-ndk-r21"
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'

    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = "../../../android-ndk-r21"
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    // Flag to tell aapt to keep the attribute ids around - (2016_02_25 add 23.2 )
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'Typos'
        checkReleaseBuilds false
        abortOnError false
    }

    // Enables Jetpack Compose for this module
    /*buildFeatures {
        compose true
    }*/

    compileOptions {
        //encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// https://developer.android.com/jetpack/androidx/versions
dependencies {

    // Kotlin
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.71"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    // Support libraries
    // https://developer.android.com/jetpack/androidx/versions
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'

    // Other
    implementation "com.squareup.okhttp3:okhttp:3.12.8"
    implementation 'com.jjoe64:graphview:4.2.2'
}

repositories {
    mavenCentral()
    google()
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}



