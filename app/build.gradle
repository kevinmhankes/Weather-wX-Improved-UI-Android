apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'

    // need $HOME/.gradle/gradle.properties to look like this
    /*
    RELEASE_STORE_FILE=/home/josh/StudioProjects/android.jks
    RELEASE_STORE_PASSWORD=pw
    RELEASE_KEY_ALIAS=android key
    RELEASE_KEY_PASSWORD=pw
    */

    signingConfigs {
        release {
            storeFile file("../../android.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "joshuatee.wx"
        minSdkVersion 21   // 5.0
        targetSdkVersion 31
        // Stops the Gradle pluginâ€™s automatic rasterization of vectors (2016_02_25 add 23.2 )
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        main {
            jni.srcDirs = [] //disable automatic ndk-build
            jniLibs.srcDir 'src/main/libs'
        }
    }

    task buildNative(type: Exec, description: 'Compile JNI source via NDK') {
        def ndkDir = "../../../android-ndk-r23"
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                '-j', Runtime.runtime.availableProcessors(),
                'all'
    }

    task cleanNative(type: Exec, description: 'Clean JNI object files') {
        def ndkDir = "../../../android-ndk-r23"
        commandLine "$ndkDir/ndk-build",
                '-C', file('src/main/jni').absolutePath,
                'clean'
    }

    clean.dependsOn 'cleanNative'

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn buildNative
    }

    // Flag to tell aapt to keep the attribute ids around - (2016_02_25 add 23.2 )
    aaptOptions {
        additionalParameters "--no-version-vectors"
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        disable 'Typos'
        checkReleaseBuilds false
        abortOnError false
    }

    // Enables Jetpack Compose for this module
    /*buildFeatures {
        compose true
    }*/

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6'
    // https://developer.android.com/jetpack/androidx/versions
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.media:media:1.4.3'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.jjoe64:graphview:4.2.2'
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    // implementation "androidx.navigation:navigation-compose:1.0.0-alpha07"
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}



